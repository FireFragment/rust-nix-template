{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    flake-parts.url = "github:hercules-ci/flake-parts";
    systems.url = "github:nix-systems/default";

    # Rust
    nci.url = "github:yusdacra/nix-cargo-integration";
    nci.inputs.nixpkgs.follows = "nixpkgs";
    nci.inputs.parts.follows = "flake-parts";

    # Dev tools
    treefmt-nix.url = "github:numtide/treefmt-nix";
  };

  outputs = inputs:
    inputs.flake-parts.lib.mkFlake { inherit inputs; } {
      systems = import inputs.systems;
      imports = [
        inputs.nci.flakeModule
        inputs.treefmt-nix.flakeModule
      ];
      perSystem = { config, self', pkgs, lib, system, ... }:
        let
          crateOutputs = config.nci.outputs."rust-nix-template";
        in
        {
          nci = {
            projects."rust-nix-template".path = ./.;
            crates = {
              "rust-nix-template" = { };
            };
          };

          # Rust package
          packages.default = crateOutputs.packages.release;

          # Rust dev environment
          devShells.default = pkgs.mkShell {
            inputsFrom = [
              crateOutputs.devShell
              config.treefmt.build.devShell
            ];
            shellHook = ''
              # For rust-analyzer 'hover' tooltips to work.
              export RUST_SRC_PATH=${pkgs.rustPlatform.rustLibSrc}

              echo
              echo "üçéüçé Run 'just <recipe>' to get started"
              just
            '';
            nativeBuildInputs = with pkgs; [
              just
              cargo-watch
            ];
          };

          # Add your auto-formatters here.
          # cf. https://numtide.github.io/treefmt/
          treefmt.config = {
            projectRootFile = "flake.nix";
            programs = {
              nixpkgs-fmt.enable = true;
              rustfmt.enable = true;
            };
          };
        };
    };
}
